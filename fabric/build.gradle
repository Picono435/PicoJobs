architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common-mod").loom.accessWidenerPath
    //systemProperty("fabric.classPathGroups", ".." + File.pathSeparator + "config" + File.pathSeparator + "PicoJobs" + File.pathSeparator + "libraries")
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

repositories {
    maven {
        url "https://maven.nucleoid.xyz/"
        name "Nucleoid"
        content {
            includeGroup 'eu.pb4'
        }
    }

    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
        name "Sonatype OSS"
        content {
            includeGroup 'me.lucko'
        }
    }
}

dependencies {
    shadowCommon(implementation(project(":common")))

    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"
    shadowCommon(modImplementation("eu.pb4:placeholder-api:2.1.1+1.20")) { transitive = false }
    shadowCommon(modImplementation("me.lucko:fabric-permissions-api:0.2-SNAPSHOT")) { transitive = false }

    common(project(path: ":common-mod", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common-mod", configuration: "transformProductionFabric")) { transitive false }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")

    dependencies {
        exclude(dependency('org.slf4j:slf4j-api:2.0.7'))
    }

    relocate 'org.spongepowered.configurate', 'com.gmail.picono435.picojobs.libs.org.spongepowered.configurate'
    relocate 'com.google.common', 'com.gmail.picono435.picojobs.libs.com.google.common'
    relocate 'com.google.errorprone', 'com.gmail.picono435.picojobs.libs.com.google.errorprone'
    relocate 'com.google.gson', 'com.gmail.picono435.picojobs.libs.com.google.gson'
    relocate 'com.google.j2objc', 'com.gmail.picono435.picojobs.libs.com.google.j2objc'
    relocate 'com.google.thirdparty', 'com.gmail.picono435.picojobs.libs.com.google.thirdparty'
    relocate 'com.typesafe.config', 'com.gmail.picono435.picojobs.libs.com.typesafe.config'
    relocate 'io.github.slimjar', 'com.gmail.picono435.picojobs.libs.io.github.slimjar'
    relocate 'io.leangen.geantyref', 'com.gmail.picono435.picojobs.libs.io.github.geantyref'
    relocate 'javax.annotation', 'com.gmail.picono435.picojobs.libs.javax.annotation'
    relocate 'org.apache.commons', 'com.gmail.picono435.picojobs.libs.org.apache.commons'
    relocate 'org.apache.maven', 'com.gmail.picono435.picojobs.libs.org.apache.maven'
    relocate 'org.checkerframework', 'com.gmail.picono435.picojobs.libs.org.checkerframework'
    relocate 'org.codehaus.plexus', 'com.gmail.picono435.picojobs.libs.org.codehaus.plexus'
    relocate 'org.yaml.snakeyaml', 'com.gmail.picono435.picojobs.libs.org.yaml.snakeyaml'
    relocate 'org.bstats', 'com.gmail.picono435.picojobs.libs.org.bstats'
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set("fabric")
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":common-mod").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}