sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()

    maven {
        url = 'https://repo.opencollab.dev/main'

        content {
            includeGroup 'cn.nukkit'
            includeGroupByRegex "com\\.nukkitx.*"
            includeGroupByRegex "org\\.iq80.*"
            includeGroup 'com.creeperface.nukkit.placeholderapi'
        }
    }

    maven {
        url = 'https://repo.opencollab.dev/maven-snapshots'

        content {
            includeGroup 'me.onebone'
        }
    }

    maven {
        url = 'https://jitpack.io'

        content {
            includeGroup 'com.github.IWareQ'
        }
    }
}

dependencies {
    // Common & API project
    shadow implementation(project(':common'))

    compileOnly('cn.nukkit:nukkit:1.0-SNAPSHOT') { transitive false }
    shadow implementation('com.github.IWareQ:FakeInventories:v1.1.4')

    // Plugins API
    compileOnly('com.creeperface.nukkit.placeholderapi:PlaceholderAPI:1.4-SNAPSHOT')
    compileOnly('me.onebone' +
            ':economyapi:2.0.0-SNAPSHOT')

}

shadowJar {
    archiveClassifier.set("nukkit")

    relocate 'org.spongepowered.configurate', 'com.gmail.picono435.picojobs.libs.org.spongepowered.configurate'
    relocate 'com.google.common', 'com.gmail.picono435.picojobs.libs.com.google.common'
    relocate 'com.google.errorprone', 'com.gmail.picono435.picojobs.libs.com.google.errorprone'
    relocate 'com.google.gson', 'com.gmail.picono435.picojobs.libs.com.google.gson'
    relocate 'com.google.j2objc', 'com.gmail.picono435.picojobs.libs.com.google.j2objc'
    relocate 'com.google.thirdparty', 'com.gmail.picono435.picojobs.libs.com.google.thirdparty'
    relocate 'com.typesafe.config', 'com.gmail.picono435.picojobs.libs.com.typesafe.config'
    relocate 'io.github.slimjar', 'com.gmail.picono435.picojobs.libs.io.github.slimjar'
    relocate 'io.leangen.geantyref', 'com.gmail.picono435.picojobs.libs.io.github.geantyref'
    relocate 'javax.annotation', 'com.gmail.picono435.picojobs.libs.javax.annotation'
    relocate 'org.apache.commons', 'com.gmail.picono435.picojobs.libs.org.apache.commons'
    relocate 'org.apache.maven', 'com.gmail.picono435.picojobs.libs.org.apache.maven'
    relocate 'org.checkerframework', 'com.gmail.picono435.picojobs.libs.org.checkerframework'
    relocate 'org.codehaus.plexus', 'com.gmail.picono435.picojobs.libs.org.codehaus.plexus'
    relocate 'org.yaml.snakeyaml', 'com.gmail.picono435.picojobs.libs.org.yaml.snakeyaml'

    relocate 'me.iwareq', 'com.gmail.picono435.libs.me.iwareq'
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 8
}

// Temporary fix to add slimjar.json and slimjar-resolutions.json files into the jar file
sourceSets {
    main {
        resources {
            srcDirs "../common/build/resources/slimjar"
        }
    }
}

project(':common').tasks.slimJar {
    dependsOn(tasks.jar)
    dependsOn(tasks.sourcesJar)
}

assemble.dependsOn shadowJar