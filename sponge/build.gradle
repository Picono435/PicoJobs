import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.plugin.metadata.model.PluginDependency

plugins {
    id("org.spongepowered.gradle.plugin") version "2.1.1"
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()

    maven {
        url 'https://repo.spongepowered.org/repository/maven-public/'

        content {
            includeGroup 'org.spongepowered'
        }
    }
}

dependencies {
    // Common & API project
    shadow implementation(project(':common'))
    // Sponge
    compileOnly 'org.spongepowered:spongeapi:8.2.0-SNAPSHOT'
    // Other libraries
    shadow implementation('org.bstats:bstats-sponge:3.0.2')
}

shadowJar {
    archiveClassifier.set("sponge")

    dependencies {
        exclude(dependency('org.apache.maven:maven-artifact:3.9.1'))
        exclude(dependency('org.slf4j:slf4j-api:2.0.7'))
    }

    relocate 'org.spongepowered.configurate', 'com.gmail.picono435.picojobs.libs.org.spongepowered.configurate'
    relocate 'com.google.common', 'com.gmail.picono435.picojobs.libs.com.google.common'
    relocate 'com.google.errorprone', 'com.gmail.picono435.picojobs.libs.com.google.errorprone'
    relocate 'com.google.gson', 'com.gmail.picono435.picojobs.libs.com.google.gson'
    relocate 'com.google.j2objc', 'com.gmail.picono435.picojobs.libs.com.google.j2objc'
    relocate 'com.google.thirdparty', 'com.gmail.picono435.picojobs.libs.com.google.thirdparty'
    relocate 'com.typesafe.config', 'com.gmail.picono435.picojobs.libs.com.typesafe.config'
    relocate 'io.github.slimjar', 'com.gmail.picono435.picojobs.libs.io.github.slimjar'
    relocate 'io.leangen.geantyref', 'com.gmail.picono435.picojobs.libs.io.github.geantyref'
    relocate 'javax.annotation', 'com.gmail.picono435.picojobs.libs.javax.annotation'
    relocate 'org.apache.commons', 'com.gmail.picono435.picojobs.libs.org.apache.commons'
    relocate 'org.checkerframework', 'com.gmail.picono435.picojobs.libs.org.checkerframework'
    relocate 'org.codehaus.plexus', 'com.gmail.picono435.picojobs.libs.org.codehaus.plexus'
    relocate 'org.yaml.snakeyaml', 'com.gmail.picono435.picojobs.libs.org.yaml.snakeyaml'
}

sponge {
    apiVersion("8.0.0")
    loader {
        name(PluginLoaders.JAVA_PLAIN)
        version("1.0")
    }
    license("MIT License")
    plugin("picojobs") {
        displayName("PicoJobs")
        version(project.version)
        entrypoint("com.gmail.picono435.picojobs.sponge.PicoJobsSponge")
        description(project.description)
        links {
            homepage("https://piconodev.com")
            source("https://github.com/Picono435/PicoJobs")
            issues("https://github.com/Picono435/PicoJobs/issues")
        }
        contributor("Picono435") {
            description("Lead Developer")
        }
        dependency("spongeapi") {
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 8
}

// Temporary fix to add slimjar.json and slimjar-resolutions.json files into the jar file
sourceSets {
    main {
        resources {
            srcDirs "../common/build/resources/slimjar"
        }
    }
}

project(':common').tasks.slimJar {
    dependsOn(tasks.jar)
    dependsOn(tasks.sourcesJar)
}

assemble.dependsOn shadowJar